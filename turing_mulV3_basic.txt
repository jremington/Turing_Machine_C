50 REM Turing Machine Simulator, multiplies integer string P by integer string Q
200 T$="PbbQQQbbbbbbbbbbbbbbbbbbbb"
210 S$="0"
220 P=1 : REM starting pos
400 PRINT T$
410 PRINT TAB(P);"^" :REM head
420 IF S$="." THEN END
499 REM state table lookup
500 M$=S$+MID$(T$,P,1)
510 RESTORE
520 READ R$ : IF MID$(R$,1,2)<>M$ THEN 520
600 PRINT S$,R$
610 A$=INKEY$ : IF A$="" THEN 610
799 REM rewrite symbol on tape
800 MID$(T$,P,1)=MID$(R$,4,1)
819 REM move left
820 IF MID$(R$,6,1)="L" THEN P=P-1
829 REM move right
830 IF MID$(R$,6,1)="R" THEN P=P+1
840 S$=MID$(R$,5,1) : REM new state
900 GOTO 400
1000 REM Multiply, no blanks before P's, any number blanks before Q's
1001 REM 4 state machine
2000 DATA "0P-E1R","0R-Q0L","0b-b0L","0E-E.."
2010 DATA "1Q-R2R","1b-b1R","1P-P1R","1T-T0L"
2020 DATA "2b-T3L","2Q-Q2R","2T-T2R"
2030 DATA "3R-R1R","3Q-Q3L","3T-T3L"
Ok
