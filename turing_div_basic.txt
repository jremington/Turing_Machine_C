50 REM Turing Machine Simulator, integer divide P string into X string (end by %) with remainder. 
51 REM at finish, result tape contains P string (original divisor), Q string (remainder), T string (result)
51 REM 
200 T$="@PPPXXXXX%bbbbbbbbbbbbbbbbbbbb"
210 S$="0"
220 P=1 : REM starting pos
400 PRINT T$
410 PRINT TAB(P);"^" :REM head
420 IF S$="." THEN END
499 REM state table lookup
500 M$=S$+MID$(T$,P,1)
510 RESTORE
520 READ R$ : IF MID$(R$,1,2)<>M$ THEN 520
600 PRINT S$,R$
610 A$=INKEY$ : IF A$="" THEN 610
799 REM rewrite symbol on tape
800 MID$(T$,P,1)=MID$(R$,4,1)
819 REM move left
820 IF MID$(R$,6,1)="L" THEN P=P-1
829 REM move right
830 IF MID$(R$,6,1)="R" THEN P=P+1
840 S$=MID$(R$,5,1) : REM new state
900 GOTO 400
2000 DATA "0@-@0R","0P-P0R","0b-b0R","0%-%..","0X-b1L","0Q-Q0R","0T-T0R"
2010 DATA "1P-Q2L","1b-b1L","1Q-Q1L","1T-T1L"
2020 DATA "2@-@3R","2P-P0R","2T-T0R"
2030 DATA "3Q-P3R","3b-T0R","3T-T3R"
Ok
